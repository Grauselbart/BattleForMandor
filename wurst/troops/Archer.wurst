package Archer

import DamageDetection
import DamageType2
import Missile
import Assets
import UnitObjEditing
import ObjectIdGenerator
import Troop

public let troopId_archer = compiletime(UNIT_ID_GEN.next())

let PROJECTILE_ART = Abilities.arrowMissile1
let PROJECTILE_SPEED = 900.
let PROJECTILE_RANGE = 900.
let PROJECTILE_ARC = (20).asAngleDegrees()
let PROJECTILE_LAUNCH_Z = 65.
let PROJECTILE_LAUNCH_ROTATION = (0.).asAngleDegrees()
let PROJECTILE_LAUNCH_OFFSET = 10.
let PROJECTILE_HITBOX = 100.

// construct super wie bei player hero
init
	addOnDamageFunc(Condition(() -> begin
		let uS = GetEventDamageSource()
		if uS.getTypeId() == troopId_archer and getDamageType() == DamageType.INGAME
			let uT = GetTriggerUnit()
			let sPos = uS.getPos()
			let startXY = sPos + ZERO2.polarOffset(uS.getFacingAngle() + PROJECTILE_LAUNCH_ROTATION, PROJECTILE_LAUNCH_OFFSET)
			let startZ = sPos.getTerrainZ() + PROJECTILE_LAUNCH_Z
			
			//Calculate damage
			let basedmg = BlzGetUnitBaseDamage(uS, 1)
			let n = BlzGetUnitDiceNumber(uS, 1)
			var roll = 0
			for i = 1 to n
				roll += GetRandomInt(1, BlzGetUnitDiceSides(uS, 1))
			
			//Create projectile
			misProjectile =	addEffect(PROJECTILE_ART, startXY)..setZ(startZ)
			misSource = uS
			misTargetPos = uT.getPos3Real()
			misSpeed = PROJECTILE_SPEED
			misAngle = startXY.angleTo(uT.getPos())
			misArc = PROJECTILE_ARC
			misMaxRange = PROJECTILE_RANGE
			misHitBox = PROJECTILE_HITBOX
			misDamageType = DamageType.ATTACK
			misHitDamage = I2R(basedmg + roll)
			Missile.registerMissile()
	end))

//OBJECT DATA
@compiletime function obj()
	new TroopDefinition(troopId_archer)
		//Base
		..setName("Archer")
		..setModelFile(Units.archer)
		..setHitPointsMaximumBase(60)
		//Attack
		..setAcquisitionRange(800)
		..setAttack1Range(800)
		..setAttack1DamageBase(12)
		..setAttack1DamageNumberofDice(2)
		..setAttack1DamageSidesperDie(12)
		..setAttack1ProjectileArt("none")
		..setAttack1WeaponType(WeaponType.Instant)